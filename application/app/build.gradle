apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'

android {
    compileSdkVersion AndroidConfig.compileSdk
    testOptions.unitTests.includeAndroidResources = true

    def currentVersion = Versioning.getVersion()

    defaultConfig {
        applicationId AndroidConfig.applicationId
        minSdkVersion AndroidConfig.minSdk
        targetSdkVersion AndroidConfig.targetSdk
        versionCode currentVersion.code
        versionName currentVersion.name
        testInstrumentationRunner AndroidConfig.instrumentationTestRunner

        vectorDrawables.useSupportLibrary = true
        vectorDrawables.generatedDensities = []

        archivesBaseName = "app-${currentVersion.name}"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding.enabled = true
}

dependencies {
    implementation project(":data")
    implementation project(":domain")

    AndroidModule.main.forEach { implementation it }
    AndroidModule.unitTesting.forEach { testImplementation it }
    AndroidModule.androidTesting.forEach { androidTestImplementation it }
}