apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def API_PUB = System.getenv("MARVEL_KEY_API_PUB")
def API_PRI = System.getenv("MARVEL_KEY_API_PRI")

android {
    compileSdkVersion AndroidConfig.compileSdk
    testOptions.unitTests.includeAndroidResources = true

    def currentVersion = Versioning.getVersion()

    defaultConfig {
        minSdkVersion AndroidConfig.minSdk
        targetSdkVersion AndroidConfig.targetSdk
        versionCode currentVersion.code
        versionName currentVersion.name
        testInstrumentationRunner AndroidConfig.instrumentationTestRunner

        buildConfigField "String", "BASE_URL", '"https://gateway.marvel.com"'
        buildConfigField 'String', 'API_PUB', "\"$API_PUB\""
        buildConfigField 'String', 'API_PRI', "\"$API_PRI\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions{
        jvmTarget = '1.8'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation project(":domain")

    AndroidModule.data.forEach { implementation it }
    AndroidModule.unitTesting.forEach { testImplementation it }
}